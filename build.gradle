plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

group = 'kr.kua'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "paper"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    maven { // MythicMobs
        name = "Lumine Releases"
        url = "https://mvn.lumine.io/repository/maven-public/"
    }

    maven { // Minedown
        url = "https://repo.minebench.de/"
    }

    maven { // ProtcolLib
        url "https://repo.dmulloy2.net/repository/public/"
    }

    maven { // https://github.com/CodeCrafter47/BungeeTabListPlus/wiki/Developers
        name = "codecrafter47-repo"
        url = "http://nexus.codecrafter47.de/content/repositories/public"
        allowInsecureProtocol = true
    }

    maven { // https://github.com/PlaceholderAPI/PlaceholderAPI/wiki/Hook-into-PlaceholderAPI
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven { // https://github.com/sgtcaze/NametagEdit/blob/master/documentation/Developers.creole
        name = "upstream"
        url = "https://ci.nametagedit.com/plugin/repository/everything"
    }
}

dependencies {
    // Libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.google.code.gson:gson:2.10.1'

    // Bukkit
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")

    // MC Plugin
    compileOnly "com.github.FrancoBM12:API-MagicCosmetics:2.2.2"
    compileOnly "io.lumine:Mythic-Dist:5.2.1"
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
//    compileOnly "codecrafter47.bungeetablistplus:bungeetablistplus-api-bukkit:2.7.0"
    compileOnly "me.clip:placeholderapi:2.11.3"
//    compileOnly 'net.luckperms:api:5.4'
    compileOnly "com.nametagedit:nametagedit:4.5.20"

    // DataBase
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'

    // Util
    implementation "io.papermc:paperlib:1.0.7"
    implementation "de.themoep:minedown-adventure:1.7.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
